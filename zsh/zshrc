zstyle :compinstall filename '/home/lewisk/.zshrc'

# completion
autoload -Uz compinit; compinit
_comp_options+=(globdots) # With hidden files

# vi mode
bindkey -v
export KEYTIMEOUT=1

# load aliases
source $DOTFILES/aliases/aliases
source $DOTFILES/aliases/kubectl
#function kubectl() { echo "+ kubectl $@">&2; command kubectl $@; }

# prompt
fpath=($DOTFILES/zsh/prompt $fpath)
autoload -Uz prompt_purification_setup; prompt_purification_setup

# cursor
fpath=($DOTFILES/zsh/plugins $fpath)
autoload -Uz vi_cursor; vi_cursor

# directory stack
setopt AUTO_PUSHD           # Push the current directory visited on the stack.
setopt PUSHD_IGNORE_DUPS    # Do not store duplicates in the stack.
setopt PUSHD_SILENT         # Do not print the directory stack after pushd or popd.
alias d='dirs -v'
for index ({0..9}) alias "$index"="cd +${index}"; unset index

#function zle-line-init zle-keymap-select {
#    RPS1=$(kube_ps1)"${${KEYMAP/vicmd/-- NORMAL --}/(main|viins)/-- INSERT --}"
#    RPS2=$RPS1
#    zle reset-prompt
#}
#zle -N zle-line-init
#zle -N zle-keymap-select

# edit command with vim. Move to its own file.
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey -M vicmd 'V' edit-command-line

# Add text objects. Move to own file. Add surrounding as well from:
# https://thevaluable.dev/zsh-install-configure-mouseless/
autoload -Uz select-bracketed select-quoted
zle -N select-quoted
zle -N select-bracketed
for km in viopp visual; do
  bindkey -M $km -- '-' vi-up-line-or-history
  for c in {a,i}${(s..)^:-\'\"\`\|,./:;=+@}; do
    bindkey -M $km $c select-quoted
  done
  for c in {a,i}${(s..)^:-'()[]{}<>bB'}; do
    bindkey -M $km $c select-bracketed
  done
done

zstyle ':completion:*:*:docker:*' option-stacking yes
zstyle ':completion:*:*:docker-*:*' option-stacking yes

source /usr/share/fzf/completion.zsh
source /usr/share/fzf/key-bindings.zsh

[ -x "$(command -v tmux)" ] \
  && [ -z "${TMUX}" ] \
  && tmux && exit
